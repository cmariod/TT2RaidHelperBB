{execcc;haspermissions}
{execcc;lib}

{if;{argslength};!=;1; {output;
```
Example: Show raid seed starting from 3-55
{prefix}{commandname} 3-55
```
}{return}}

{set;~isstarted;false}
{set;~count;0}

{set; ~embeds; []}
{set; ~embed; {j;{"color":16316671}}}
{//; set; ~embed; {j;{}}}
{//; jset; ~embed; color; 16316671 ;create}
{jset; ~embed; title; Raid List Preview ;create}
{jset; ~embed; footer.text; Seed: {get;_{category}loadedseed} ;create}

{set; ~embedfields; []}

{foreach;~raidtierzone;{jvalues;_{category}seed};
  {if;{jget; ~raidtierzone; tier}-{jget; ~raidtierzone; level};==;{args;0};{set;~isstarted;true}}
  {if;{get;~count};==;8;{set;~isstarted;false}}
  {if;{get;~isstarted};
    {set;~tier;{jget; ~raidtierzone; tier}}
    {switch; {get;~tier};
      ["3","4","5"];
        {set; ~embedfield; {j;{}}}
        {jset; ~embedfield; name; {jget; ~raidtierzone; tier}-{jget; ~raidtierzone; level};create}
        {jset; ~embedfield; value; {if;{func.jkeyexists;~raidtierzone;area_buffs};{func.buffformat;{jget;~raidtierzone;area_buffs.0}};No Buff}{newline}{join;{map;~item;{jget; ~raidtierzone; spawn_sequence};{func.titanemoji;{get;~item}}};};create}
        {jset; ~embedfield; inline; true; create}
        {push; ~embedfields; {get; ~embedfield}}

        {set; ~embedfield; {j;{}}}
        {jset; ~embedfield; name; Titan & Curse ; create}
        {jset; ~embedfield; value; {foreach;~titan;{jget;~raidtierzone;titans};
          {if;{func.jkeyexists;~titan;cursed_debuffs};
            {func.titanemoji;{jget;~titan;enemy_name}}{space}{func.curseacro;{jget;~titan;cursed_debuffs.0.bonus_type}}{space}{set; ~raidtitancurseparts; []}{foreach;~elem;{jget; ~titan;parts};{if;{jget;~elem;cursed};{push;~raidtitancurseparts;{jget;_{category}raiddata;Parts.{jget;~elem;part_id}}}}}{sort; ~raidtitancurseparts}({join;{map;~item;{get;~raidtitancurseparts};{func.partidtoacro;{get;~item}}};,{space}}){space};No Curse
          }{newline}
        };create}
        {jset; ~embedfield; inline; true; create}
        {push; ~embedfields; {get; ~embedfield}}

        {set; ~embedfield; {j;{}}}
        {jset; ~embedfield; name; Debuff ;create}
        {jset; ~embedfield; value; {foreach;~titan;{jget;~raidtierzone;titans};
          {if;{func.jkeyexists;~titan;area_debuffs};{func.debuffformat;{jget;~titan;area_debuffs.0}};No Debuff}{newline}
        };create}
        {jset; ~embedfield; inline; true; create}
        {push; ~embedfields; {get; ~embedfield}}
      ;
    }
    {increment;~count}
  }
}

{jset; ~embed; fields; {get; ~embedfields} ;create}

{output;{embed;{jclean;{get; ~embed}}}}