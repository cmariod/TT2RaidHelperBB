{execcc;haspermissions}
{execcc;lib}

{if;{argslength};==;0;
```
Command to set strategy for specific titan. Strategy follows part id sequence: 23456789, refer to part number below, 0 to skip and 1 to kill

    â”Œâ”€â”€â”€â”
â”Œâ”€â”€â”€â”¤ 2 â”œâ”€â”€â”€â”
â”‚ 4 â”œâ”€â”€â”€â”¤ 9 â”‚
â”œâ”€â”€â”€â”¤ 3 â”œâ”€â”€â”€â”¤
â”‚ 5 â”‚   â”‚ 8 â”‚
â””â”€â”¬â”€â”´â”€â”¬â”€â”´â”€â”¬â”€â”˜
  â”‚ 6 â”‚ 7 â”‚
  â””â”€â”€â”€â”´â”€â”€â”€â”˜

Usage:
{prefix}{commandname} {realpad;default;40}: shows default titan strategy for all
{prefix}{commandname} {realpad;<raid_tier_zone>;40}: shows default titan strategy for specific raid_tier_zone
{prefix}{commandname} {realpad;<titan_name> [raid_tier_zone] <strategy>;40}: set strategy for particular titan. when raid_tier_zone is not defined, strategy will be set as default
{prefix}{commandname} {realpad;<titan_name> <raid_tier_zone> default;40}: set strategy for particular titan and raid_tier_zone back to default strategy

Example to Set Mohaca to not hit chest(3) by default:
{prefix}{commandname} mo 10111111

Example to Set Mohaca for currently loaded raid 4-1's Debuff to not hit head(2) and chest(3):
{prefix}{commandname} mo 4-1 00111111
```{return}
}

{//;default strategy}
{if;{length;{get;_strat}};0;{set;_strat;{j;{"Default":{"Enemy1":"11111111","Enemy2":"11111111","Enemy3":"10111111","Enemy4":"01111111","Enemy5":"10111111","Enemy6":"01111111","Enemy7":"11111111","Enemy8":"11111111"}}}}}

{if;{argslength};==;1;
  {if;{lower;{args; 0}};==;default;
    {set; ~embed; {j;{}}}
    {jset; ~embed; title; Titan Strategy List ;create}
    {jset; ~embed; description; (O)Hit (X)Avoid ;create}
    {set; ~embedfields; []}
    {for;~index;0;<;8;
      {set; ~embedfield; {j;{}}}
      {set; ~titanstrategy; {split;{regexreplace;{regexreplace;{jget;{jvalues;_strat;Default};{get; ~index}};/1/gi;O};/0/gi;X};}}
      {jset; ~embedfield; name; {func.titanemoji;{jget;_raiddata;Enemies.{jget;{jkeys;_strat;Default};{get; ~index}}}} {jget;_raiddata;Enemies.{jget;{jkeys;_strat;Default};{get; ~index}}} ;create}
      {jset; ~embedfield; value; {jget;~titanstrategy;2} {jget;~titanstrategy;0} {jget;~titanstrategy;7}{newline}{jget;~titanstrategy;3} {jget;~titanstrategy;1} {jget;~titanstrategy;6}{newline}Â Â Â {jget;~titanstrategy;4} {jget;~titanstrategy;5} ;create}
      {jset; ~embedfield; inline; true ;create}
      {push; ~embedfields; {get; ~embedfield}}
    }

    {//; filler because to fill in nicely 3 x 3 boxes}
    {set; ~embedfield; {j;{}}}
    {jset; ~embedfield; name; . ;create}
    {jset; ~embedfield; value; . ;create}
    {jset; ~embedfield; inline; true ;create}
    {push; ~embedfields; {get; ~embedfield}}
    
    {jset; ~embed; fields; {get; ~embedfields} ;create}
    {output; {embed;{jclean;{get; ~embed}}}}
    {return}
  ;
    {set; ~embed; {j;{}}}
    {jset; ~embed; title; Titan Strategy List for {args;0};create}
    {jset; ~embed; description; {func.buffformat;{jget;_seed;{args;0}.area_buffs.0}} ;create}
    {jset; ~embed; footer.text; (O)Hit (X)Avoid ;create}
    {set; ~embedfields; []}
    {foreach;~titan;{jget;_seed;{args;0}.titans};
      {set; ~enemyname; {jget;~titan;enemy_name}}
      {set; ~debuffid; {func.buffid;{jget;~titan;area_debuffs.0}}}
      {set; ~basehp;{jget;~titan;total_hp}}
      {set; ~enemyid; {jget; ~titan; enemy_id}}
      {set; ~titanstrategy; {split;{func.stratformat;{jget;_strat;{if;{func.jkeyexists;_strat;{get;~debuffid}.{get;~enemyid}};{get;~debuffid};Default}.{get;~enemyid}}};}}
      {set; ~partstrategy; {concat;["0", "0"]; {split; {jget;_strat;{if;{func.jkeyexists;_strat;{get;~debuffid}.{get;~enemyid}};{get;~debuffid};Default}.{get;~enemyid}};}}}

      {set;~body;[]}
      {set;~armor;[]}
      {set;~bodypartsonstrat;[]}
      {set;~raidtitancurseparts; []}
      {set;~totalbody;0}
      {set;~allbody;0}
      {set;~totalarmor;0}
      {set;~allarmor;0}
      {foreach;~part;{jget;~titan;parts};
        {set;~partjson;{j}}
        {jset;~partjson;id;{jget;_raiddata;Parts.{jget;~part;part_id}};create}
        {jset;~partjson;hp;{jget;~part;total_hp};create}

        {if;{jget;~part;cursed}; {push;~raidtitancurseparts;{jget;_raiddata;Parts.{jget;~part;part_id}}}}

        {if;{jget;~part;part_id};startswith;Body;
          {push;~body;{get;~partjson}}
          {increment;~allbody;{jget;~part;total_hp};false}
          {if;{jget;~partstrategy;{substring;{jget;~partjson;id};0;1}};>;0;{increment;~totalbody;{jget;~part;total_hp};false}{push;~bodypartsonstrat;{jget;~part;total_hp}}}
        ;
          {push;~armor;{get;~partjson}}
          {increment;~allarmor;{jget;~part;total_hp};false}
          {if;{jget;~partstrategy;{substring;{jget;~partjson;id};0;1}};>;0;{increment;~totalarmor;{jget;~part;total_hp};false}}
        }
      }

      {sort; ~raidtitancurseparts}
      {set; ~curseparts; {split;NNNNNNNN;}}
      {foreach; ~elem; {get; ~raidtitancurseparts};
        {jset;~curseparts;
          {math;-;{parseint;{shift;{split;{get;~elem};}}};2}
        ;C}
      }

      {set;~totaldamage;{math;+;{get;~basehp};{get;~totalarmor}}}
      {jsort;{get;~body};id}
      {jsort;{get;~armor};id}

      {set;~vmbodyhp;{math;-;
        {max;{get;~bodypartsonstrat}}
      ;
        {if;{get;~totalbody};>;{get;~basehp};{math;-;{get;~totalbody};{get;~basehp}};0}
      }}
      
      {set; ~embedfield; {j}}
      {jset; ~embedfield; name; {func.titanemoji;{get; ~enemyname}} {get; ~enemyname} ({func.curseacro;{jget;~titan;cursed_debuffs.0}} Cursed) {if; {get; ~totalbody}; <; {get;~basehp}; ğŸ†˜} ;create}
      {jset; ~embedfield; value; {func.debuffformat;{jget;~titan;area_debuffs.0}}``` {jget;~curseparts;2} {jget;~curseparts;0} {jget;~curseparts;7}    |    {jget;~titanstrategy;2} {jget;~titanstrategy;0} {jget;~titanstrategy;7}{newline} {jget;~curseparts;3} {jget;~curseparts;1} {jget;~curseparts;6}    |    {jget;~titanstrategy;3} {jget;~titanstrategy;1} {jget;~titanstrategy;6}{newline}  {jget;~curseparts;4} {jget;~curseparts;5}     |     {jget;~titanstrategy;4} {jget;~titanstrategy;5}{newline}Base HP:  {realpad;{numformat;{math;/;{get;~basehp};1000000};2}M;11;{space};left}{newline}Eff Body:  {realpad;{numformat;{math;/;{get;~totalbody};1000000};2}M;10;{space};left}{newline}Eff Armor: {realpad;{numformat;{math;/;{get;~totalarmor};1000000};2}M;10;{space};left}{newline}Eff VMBody: {realpad;{numformat;{math;/;{get;~vmbodyhp};1000000};2}M;9;{space};left}{newline}Total Dmg: {realpad;{numformat;{math;/;{get;~totaldamage};1000000};2}M;10;{space};left}```;create}
      {jset; ~embedfield; inline; true ;create}
      {push; ~embedfields; {get; ~embedfield}}

      {set; ~embedfield; {j}}
      {jset; ~embedfield; name; Body ; create}
      {jset; ~embedfield; value; ```{foreach;~part;{get;~body};
        {func.partidtoacro;{jget;~part;id}}: {realpad;{numformat;{math;/;{jget;~part;hp};1000000};2}M;10;{space};left}{newline}
      }Ttl: {realpad;{numformat;{math;/;{get;~allbody};1000000};2}M;9;{space};left}```;create}
      {jset; ~embedfield; inline; true; create}
      {push; ~embedfields; {get; ~embedfield}}

      {set; ~embedfield; {j}}
      {jset; ~embedfield; name; Armor ; create}
      {jset; ~embedfield; value; ```{foreach;~part;{get;~armor};
        {func.partidtoacro;{jget;~part;id}}: {realpad;{numformat;{math;/;{jget;~part;hp};1000000};2}M;10;{space};left}{newline}
      }Ttl: {realpad;{numformat;{math;/;{get;~allarmor};1000000};2}M;9;{space};left}```;create}
      {jset; ~embedfield; inline; true; create}
      {push; ~embedfields; {get; ~embedfield}}
    }
    {jset; ~embed; fields; {get; ~embedfields} ;create}
    {output; {embed;{jclean;{get; ~embed}}}}
    {return}
  }
}

{set;~stratset;{args; {math;-;{argslength};1}}}

{if;{argslength};==;2;
  {set; ~titanname; {shift;{filter;~element;{jvalues; _raiddata; Enemies};{bool;{get;~element};startswith;{capitalize;{args; 0}}}}}}
  {set; ~matchidx; {indexof; {jvalues; _raiddata; Enemies}; {get; ~titanname}}}
  {set; ~enemyid; {jget; {jkeys; _raiddata; Enemies}; {get; ~matchidx}}}
  {set; ~debuff;Default}
;
  {set; ~titan; {shift;{filter;~element;{jget; _seed; {args;1}.titans};{bool;{capitalize;{jget;~element;enemy_name}};startswith;{capitalize;{args; 0}}}}}}
  {set; ~enemyid; {jget; ~titan; enemy_id}}
  {set; ~debuff;{func.buffid;{jget;~titan;area_debuffs.0}}}
  {set; ~titanname; {jget; ~titan; enemy_name}}
}

{if; {lower;{get;~stratset}}; ==; default; {set;~stratset; {jget; _strat; Default.{get; ~enemyid}}}}

{if; {length; {get;~stratset}}; !=; 8; {output; Missing part in strategy}{return}}

{jset; _strat; {get;~debuff}.{get; ~enemyid}; {get;~stratset}; create}

{output; {get; ~titanname} {if;{argslength};==;2;Default;{func.debuffformat;{jget;~titan;area_debuffs.0}}} strategy set to {capitalize;{get;~stratset};lower}}