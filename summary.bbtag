{execcc;haspermissions}

{ if; {logic; ||; { flagset; h }; {bool; {length; {flagsarray}}; ==; 1}}; {output;
```
Parameters:
  -h  Show this help
  -d  {realpad;<buffedclanaverage>;25} BUFFed clan average, e.g. 5.5
  -x  {realpad;Where x is between 1 to 8;25} This will set full void for the particular titan in sequence
      e.g. -1 -4 ==> Full void for terro at position 1, 4
```
}{return}}

{if; {length; {get; _raidtierzone}}; ==; 0; {output;Raid not set, please {prefix}raidset first}}

{set; ~summarydata; {jclean;{jget; _cache; {get; _raidtierzone}}}}
{set; ~computeddata; {jclean;{jget; _cache; {get; _raidtierzone}.computed}}}

{//; calculate cycle prediction here}
{set; ~raidtier; {shift; {split; {get; _raidtierzone}; -}}}
{if; {get; ~raidtier}; ==; 4;
  {set; ~hitspercycle; 5}
; 
  {set; ~hitspercycle; 4}
}

{set; ~bavgmil; {if; {flagset;d}; 
  {flag; d}
;
  {jget; ~computeddata; bavg}
}}
{set; ~bavg; {math;*;{parsefloat;
  {get; ~bavgmil}
};1000000}} {jset; _cache; summary.bavg; {get; ~bavg}; create}
{set; ~titansequence; {split;{jget; ~summarydata; flags};}}
{set; ~totaltitans; {length;{get;~titansequence}}}
{set; ~basehp; {jget; ~computeddata; basehp}} {jset; _cache; summary.basehp; {get; ~basehp}; create}
{set; ~basehphits; {math;/;
  {parsefloat;{get;~basehp}};
  {parsefloat;{get;~bavg}}
}} {jset; _cache; summary.basehphits; {get; ~basehphits}; create}
{set; ~totalhitpercycle; {math;*;{get; _activeraidpower};{get;~hitspercycle}}}

{set; ~overflowdmg; 0}
{set; ~hitsused; 0}
{set; ~totalhitsincycle; 0}

{set; ~phases; ["Prism", "IV", "VM", "dead"]}
{set; ~currentcycle; 0}
{set; ~cycledetails; [{j;{}}]}
{set; ~cycleremaininghits; {get; ~totalhitpercycle}}
{set; ~hitsusedlabel; 0}

{for;~index;0;<;{get; ~totaltitans};
  {set; ~enemyid; {jget;~titansequence;{get;~index}}}
  {set; ~titanhp; {jget; ~computeddata; titan{get;~enemyid}.hp}}

  {jset; ~cycledetails; {get;~currentcycle}.titans.{get;~index}.label; {jget; ~computeddata; titan{get;~enemyid}.name}; create}
  
  {set; ~titanhpnonvma; {parsefloat;{jget; ~computeddata; titan{get;~enemyid}.hpprcnta}} }
  {decrement; ~titanhpnonvma; 
    {if; {flagset; {math;+;{get; ~index};1}};
      0;
      {parsefloat;{jget; ~computeddata; titan{get;~enemyid}.vm.hpprcnta}}
    }
  ; false}
  
  {set; ~titanhitsnonvma; {math;*;
    {get;~titanhpnonvma};
    {get;~basehphits}
  }}
  {jset; _cache; summary.titan{get;~enemyid}.hits.nonvma; {get;~titanhitsnonvma}; create}
  
  {set; ~titanhpnonvmb; {math;-;
    {parsefloat;{jget; ~computeddata; titan{get;~enemyid}.hpprcntb}};
    {parsefloat;{jget; ~computeddata; titan{get;~enemyid}.vm.hpprcntb}}
  }}
  {set; ~titanhitsnonvmb; {math;*;
    {get;~titanhpnonvmb};
    {get;~basehphits}
  }}
  {jset; _cache; summary.titan{get;~enemyid}.hits.nonvmb; {get;~titanhitsnonvmb}; create}
  
  {set; ~titanhitsvma; {if; {flagset; {math;+;{get; ~index};1}};
      0;
      {math;*;
        {parsefloat;{jget; ~computeddata; titan{get;~enemyid}.vm.hpprcnta}};
        {get;~basehphits}
      }
  }}
  {set; ~titanhitsvmb; {math;*;
    {math;-;
      {parsefloat;1.0};
      {parsefloat;{get;~titanhpnonvmb}}
    };
    {get;~basehphits}
  }}
  {set; ~titanhitsvm; {math;+;
    {get; ~titanhitsvma};
    {get; ~titanhitsvmb}
  }}
  {jset; _cache; summary.titan{get;~enemyid}.hits.vma; {get;~titanhitsvma}; create}
  {jset; _cache; summary.titan{get;~enemyid}.hits.vmb; {get;~titanhitsvmb}; create}
  {jset; _cache; summary.titan{get;~enemyid}.hits.vm; {get;~titanhitsvm}; create}

  {set; ~titanbodyhitsleft; {get;~basehphits}}
  
  {set; ~cyclesteps; []}
  {foreach;~phase;~phases;
    {switch;{get;~phase};
      Prism;
        {set; ~hitsused; 0}
        {set; ~emoji; <:Prism:859797057102544927>}
      ;
      
      IV;
        {set; ~hitsused; {get;~titanhitsnonvma}}
        {set; ~hitsusedlabel; {get;~hitsused}}
        {set; ~emoji; <:IV:859792058205077524>}
      ;
      
      VM;
        {set; ~hitsused; {get;~titanhitsnonvmb}}
        {set; ~hitsusedlabel; {get;~hitsused}}
        {set; ~emoji; <:VM:859792074672832512>}
        {decrement; ~titanbodyhitsleft; {get; ~hitsused}; false}
      ;
      
      dead;
        {set; ~hitsused; {get;~titanhitsvm}}
        {set; ~hitsusedlabel; {get;~hitsused}}
        {set; ~emoji; <:dead:859792109861863444>}
      ;
    }
    {increment; ~totalhitsincycle; {get;~hitsused}; false}
    
    {if;{get;~cycleremaininghits};>=;{get;~hitsused};
      {set; ~hitcycleprcnt; {math;/; {ceil;{get;~totalhitsincycle}}; {get; ~totalhitpercycle}}}
      {set; ~hitcycleprcnt; {if; {get;~hitcycleprcnt}; <=; 0.4;Early;{if; {get;~hitcycleprcnt}; <=; 0.7;Mid;Late}}}
      
      {push; ~cyclesteps; {repeat;{space};6}{get; ~emoji} {get; ~hitcycleprcnt} ({ceil;{get;~totalhitsincycle}}){space}}
    ;
      {//; next cycle}
      {//push; ~cyclesteps; {repeat;{space};6}{get; ~emoji} {get; ~hitcycleprcnt} ({ceil;{get;~totalhitsincycle}}){space}}
      
      {decrement; ~hitsusedlabel; {get;~cycleremaininghits}; false}
      {decrement; ~totalhitsincycle; {get; ~totalhitpercycle}; false}
      {increment; ~cycleremaininghits; {get; ~totalhitpercycle}; false}

      {switch;{get;~phase};
        VM;
          {increment; ~titanbodyhitsleft; {get; ~totalhitsincycle}; false}
          {push; ~cyclesteps; Remaining HP: {numformat;
            {math;/;{math;*;{get; ~titanbodyhitsleft};{parsefloat;{get;~bavg}}};1000000}
          ;2}M}
          {decrement; ~titanbodyhitsleft; {get; ~totalhitsincycle}; false}
        ;
        
        dead;
          {push; ~cyclesteps; Remaining HP: {numformat;
            {math;/;
              {math;*;{min;{get;~titanhitsvmb};{get; ~totalhitsincycle}};{parsefloat;{get;~bavg}}}
              ;1000000}
          ;2}M}
        ;
        
        {push; ~cyclesteps; Remaining HP: {numformat; {math;/;{get;~basehp};1000000};2}M}
      }
      
      {jset; ~cycledetails; {get;~currentcycle}.titans.{get;~index}.steps; {get; ~cyclesteps}; create}
      {set; ~cyclesteps; []}
      
      {set; ~hitcycleprcnt; {math;/; {ceil;{get;~totalhitsincycle}}; {get; ~totalhitpercycle}}}
      {set; ~hitcycleprcnt; {if; {get;~hitcycleprcnt}; <=; 0.4;Early;{if; {get;~hitcycleprcnt}; <=; 0.7;Mid;Late}}}
      
      {push; ~cyclesteps; {repeat;{space};6}{get; ~emoji} {get; ~hitcycleprcnt} ({ceil;{get;~totalhitsincycle}}){space}}
      
      {increment; ~currentcycle}
      {push; ~cycledetails; {j;{}}}
      {jset; ~cycledetails; {get;~currentcycle}.titans.{get;~index}.label; {jget; ~computeddata; titan{get;~enemyid}.name}; create}
    }
    {decrement; ~cycleremaininghits; {get;~hitsused}; false}
  }
  
  {jset; ~cycledetails; {get;~currentcycle}.titans.{get;~index}.steps; {get; ~cyclesteps}; create}
}

{jset; _cache; summary.cycledetails; {get;~cycledetails}; create}

{output;Strategy for {get; _raidtierzone} at {numformat;{get; ~bavgmil};2}M average DMG:

Raid Buff : {jget; ~computeddata; buff.text}
Morale Buff : +{get; _activeraidmorale}%
Debuffs:
{repeat;{space};6}{realpad;{jget; ~computeddata; titan1.name};8}: {jget; ~computeddata; debuff1.text}
{repeat;{space};6}{realpad;{jget; ~computeddata; titan2.name};8}: {jget; ~computeddata; debuff2.text}
{repeat;{space};6}{realpad;{jget; ~computeddata; titan3.name};8}: {jget; ~computeddata; debuff3.text}
{for;~index;0;<;{length;{get;~cycledetails}};
{newline}Cycle {math;+;{get;~index};1}{newline}{foreach;~element;{jkeys; ~cycledetails; {get;~index}.titans};{set; ~steps; {jget; ~cycledetails; {get;~index}.titans.{get;~element}.steps}}{jget; ~cycledetails; {get;~index}.titans.{get;~element}.label} (#{math;+;{get;~element};1}/{get;~totaltitans}):
{foreach;~step;~steps;
{get;~step}{newline}
}
}
}
}